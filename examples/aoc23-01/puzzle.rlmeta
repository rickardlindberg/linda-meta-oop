actor Main = "Args" -> selftest()

universe Part1 =
    CLI()
    Solver()
    Lines()
    Numbers()
    examples
        ["Args" "example1.txt"] -> [["Result" 142]]
        ["Args" "input.txt"] -> [["Result" 54388]]

universe Part2 =
    CLI()
    Solver2()
    Lines()
    Numbers()
    examples
        ["File" "843329\n"] -> [["Result" 89]]
        ["File" "5oneightfg\n"] -> [["Result" 58]]
        ["File" "one\n"] -> [["Result" 11]]
        ["File" "oneight\n"] -> [["Result" 18]]
        ["Args" "example2.txt"] -> [["Result" 281]]
        ["Args" "input.txt"] -> [["Result" 53515]]

actor CLI =
    "Args" .:input !. -> put(["File" read(input)])

actor Solver =
    "File" [input:x] -> put(["Numbers" ~x])
    where
        input = line*:xs !. -> xs
        line = char*:xs '\n' -> [~~xs]
        char =
            | '0'-'9':x -> [int(x)]
            | !'\n' .   -> []

actor Solver2 =
    "File" [input:x] -> put(["Lines" ~x])
    where
        input = line*:xs !. -> xs
        line = (!'\n' .)*:xs '\n' -> [xs xs]

actor Lines =
    "Lines" line*:xs !. -> put(["Numbers" ~xs])
    where
        line = [[nextDigit:x] [(. !(!nextDigit))* nextDigit:y]] -> [x y]
        nextDigit =
            | 'one'             -> 1
            | 'two'             -> 2
            | 'three'           -> 3
            | 'four'            -> 4
            | 'five'            -> 5
            | 'six'             -> 6
            | 'seven'           -> 7
            | 'eight'           -> 8
            | 'nine'            -> 9
            | '1'-'9':x         -> x
            | !'\n' . nextDigit

actor Numbers =
    "Numbers" item*:xs !. -> put(["Result" sum(xs)])
    where
        item = [add:x] -> x
        add =
            | .:x (. !(!.))* .:y -> int({ x y })
            | .:x                -> int({ x x })

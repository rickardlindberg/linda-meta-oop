actor Main = "Args" -> selftest()

actor CLI =
    "Args" .:input !. -> put(["File" read(input)])

actor Parser =
    "File" [grids:x] !. -> put(x)
    where
        grids = grid:x ('\n' grid)*:xs !. -> ["Grids" x ~xs]
        grid  = line*:xs                  -> ["Grid"
                                                ["Rows" ~xs]
                                                ["Columns" ~transpose(xs)]]
        line  = char:x char*:xs '\n'      -> { x xs }
        char  = '#' | '.'

def transpose rows =
    assert len(set(len(x) for x in rows)) == 1
    return [
        "".join(rows[row][column] for row in range(len(rows)))
        for column in range(len(rows[0]))
    ]

universe ParserExamples =
    Parser()
    examples
        ["File" "#.\n##\n"] -> [
            ["Grids"
                ["Grid"
                    ["Rows"    "#." "##"]
                    ["Columns" "##" ".#"]]]]

actor ReflectionFinder =
    "Grids" grid*:xs !. -> put(["Reflections" ~xs])
    where
        grid = ["Grid" ["Rows" .*:rows] ["Columns" .*:columns] !.] ->
            sumReflect(rows columns)

def sumReflect rows columns =
    return 100*reflectIndex(rows) + reflectIndex(columns)

def reflectIndex items =
    lines = []
    for line in range(1, len(items)):
        if isPerfectReflection(items, line):
            lines.append(line)
    assert len(lines) in [0, 1, 2]
    return max([0]+lines)

def isPerfectReflection items line =
    start = line - 1
    end = line
    while start >= 0 and end < len(items):
        indices = diffIndices(items[start], items[end])
        if indices:
            return False
        start -= 1
        end += 1
    return True

def diffIndices left right =
    assert len(left) == len(right)
    return [
        index
        for index in range(len(left))
        if left[index] != right[index]
    ]

universe ReflectionFinderExamples =
    ReflectionFinder()
    examples
        ["Grids"
            ["Grid"
                ["Rows"    "##" "##"]
                ["Columns" "##" "##"]]
            ["Grid"
                ["Rows"    "##" "##"]
                ["Columns" "##" "##"]]] ->
            [["Reflections" 101 101]]

actor ReflectionSmudgeFinder =
    "Grids" grid*:xs !. -> put(["Reflections" ~xs])
    where
        grid = ["Grid" ["Rows" .*:rows] ["Columns" .*:columns] !.] ->
            sumReflectSmudge(rows columns)

def sumReflectSmudge rows columns =
    for row in range(1, len(rows)):
        index = isPerfectReflectionSmudge(rows, row)
        if index is not None:
            row_index, column_index, line = index
            return line*100
    for column in range(1, len(columns)):
        index = isPerfectReflectionSmudge(columns, column)
        if index is not None:
            column_index, row_index, line = index
            return line
    assert False, "no smudge"

def isPerfectReflectionSmudge items line =
    start = line - 1
    end = line
    smudge = None
    while start >= 0 and end < len(items):
        indices = diffIndices(items[start], items[end])
        if indices:
            if len(indices) == 1 and smudge is None:
                smudge = (start, indices[0], line)
            else:
                return None
        start -= 1
        end += 1
    return smudge

actor Summer =
    "Reflections" .*:xs !. -> put(["Result" sum(xs)])

universe Part1 =
    CLI()
    Parser()
    ReflectionFinder()
    Summer()
    examples
        ["Args" "example.txt"] -> [["Result" 405]]
        ["Args" "input.txt"] -> [["Result" 31877]]

universe Part2 =
    CLI()
    Parser()
    ReflectionSmudgeFinder()
    Summer()
    examples
        ["Args" "example.txt"] -> [["Result" 400]]
        ["Args" "input.txt"] -> [["Result" 42996]]

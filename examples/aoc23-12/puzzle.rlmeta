actor CLI =
    "Args" .:input !. -> put(["File" read(input)])

actor Parser =
    "File" [records:x] !. -> put(x)
    where
        records = record*:xs !.                 -> ["Records" ~xs]
        record  = state*:xs ' ' numbers:ys '\n' -> ["Record" { xs } ys]
        state   = '?' | '.' | '#'
        numbers = number:x (',' number)*:xs     -> [x ~xs]
        number  = '0'-'9':x '0'-'9'*:xs         -> int({ x xs })

actor CountArrangements =
    "Records" record*:xs !. -> put(["PlacementCounts" ~xs])
    where
        record = ["Record" .:pattern .:groups !.] -> count(pattern groups)

def count pattern groups =
    return countInner(pattern, groups, 0)

def countInner pattern groups pos =
    if groups:
        count = 0
        for offset in range(len(pattern[pos:])-sum(groups)-len(groups)+2):
            if canPlaceBrokenSection(pattern, pos, offset, groups[0]):
                count += countInner(pattern, groups[1:], pos+offset+groups[0]+1)
        return count
    else:
        if "#" not in pattern[pos:]:
            return 1
        else:
            return 0

def canPlaceBrokenSection pattern pos offset size =
    start = pos + offset
    end = start + size
    if "#" in pattern[pos:start]:
        return False
    return "." not in pattern[start:end] and pattern[end:end+1] != "#"

actor Summer =
    "PlacementCounts" .*:xs !. -> put(["Result" sum(xs)])

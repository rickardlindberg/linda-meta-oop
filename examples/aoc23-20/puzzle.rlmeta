universe Part1 =
    CLI()
    Parser()
    ConjunctionAugmenter()
    PulseCounter()
    //examples
    //    ["Args" "example.txt"] -> [["Pulses" 8 4]]

actor CLI =
    "Args" .:input !. -> put(["File" read(input)])

actor Parser =
    "File" [config:x !.] !. -> put(x)
    where
        config  = module*:xs             -> ["Configuration" ~xs]
        module  = type:t name:x ' -> '
                  targets:xs '\n'        -> [t x xs]
        targets = name:x (', ' name)*:xs -> [x ~xs]
        name    = char:x char*:xs        -> { x xs }
        char    = 'a'-'z'
        type    =
            | '%' -> "FlipFlop"
            | '&' -> "Conjunction"
            |     -> "Module"
    examples
        ["File" {
            "broadcaster -> a, b, c\n"
            "%a -> b\n"
            "%b -> c\n"
            "%c -> inv\n"
            "&inv -> a\n"
        }] -> [["Configuration"
            ["Module" "broadcaster" ["a" "b" "c"]]
            ["FlipFlop" "a" ["b"]]
            ["FlipFlop" "b" ["c"]]
            ["FlipFlop" "c" ["inv"]]
            ["Conjunction" "inv" ["a"]]
        ]]

actor ConjunctionAugmenter =
    "Configuration" module*:xs
        -> dict():ins
        -> put(["Program" ~xs])
    where
        module =
            | ["Conjunction":type .:name .:out]
                -> [type name ["In" ~readIn(ins name)] ["Out" ~out]]
            | [.:type .:name [out*:outs]]
                -> name:fromName
                -> [type name outs]
        out = .:toName
            -> registerTransition(ins fromName toName)
            -> toName
    examples
        ["Configuration"
            ["Module" "broadcaster" ["a" "b"]]
            ["FlipFlop" "a" ["b"]]
            ["FlipFlop" "b" ["inv"]]
            ["Conjunction" "inv" ["a"]]
        ] -> [
            ["Program"
                ["Module" "broadcaster" ["a" "b"]]
                ["FlipFlop" "a" ["b"]]
                ["FlipFlop" "b" ["inv"]]
                ["Conjunction" "inv" ["In" "b"] ["Out" "a"]]
            ]
        ]

actor PulseCounter =
    | "Program" module*:xs !.
        -> put(["Pulser" [["broadcaster" "low" "-"]] dict([~~xs]) 1 0])
    | "Pulser" [!.] .:state .:low .:high !.
        -> put(["Pulses" low high])
    where
        module =
            | ["Module" .:name .:outs]
                -> spawn(Forwarder(name outs))
                -> []
            | ["FlipFlop" .:name .:outs]
                -> spawn(FlipFlopper(name outs))
                -> [[{"onoff_" name} "off"]]
            | ["Conjunction" .:name ["In" in*:ins] ["Out" .*:outs]]
                -> [[{"last_" name} dict(ins)]]
        in = .:x -> [x "low"]
    examples
        ["Program"
            ["Module" "broadcaster" ["a"]]
            ["FlipFlop" "a" ["a"]]
        ] -> [
            ["Pulses" 2 1]
        ]

actor FlipFlopper #name #outs =
    | "Pulser" [[#name "low"] .*:xs] .:state .:low .:high !.
        -> flip(state name outs):x
        -> put(["Pulser"
               [~xs ~get(x "pulses")]
               get(x "state")
               add(low get(x "lowCount"))
               add(high get(x "highCount"))
           ])
    | "Pulser" [[#name "high"] .*:xs] .:state .:low .:high !.
        -> put(["Pulser" xs state low high])

actor Forwarder #name #outs =
    | "Pulser" [[#name "low"] .*:xs] .:state .:low .:high !.
        -> put(["Pulser"
               [~xs ~pulseOut(outs "low")]
               state
               add(low 1)
               high
           ])
    | "Pulser" [[#name "high"] .*:xs] .:state .:low .:high !.
        -> put(["Pulser"
               [~xs ~pulseOut(outs "high")]
               state
               low
               add(high 1)
           ])

def add x y =
    return x + y

def flip state name outs =
    lowCount = 0
    highCount = 0
    key = f"onoff_{name}"
    if state[key] == "on":
        pulse = "low"
        flipped = "off"
        lowCount += 1
    elif state[key] == "off":
        pulse = "high"
        flipped = "on"
        highCount += 1
    else:
        raise ValueError("invalid state")
    return {
        "state": dict(state, **{key: flipped}),
        "lowCount": lowCount,
        "highCount": highCount,
        "pulses": pulseOut(outs, pulse),
    }

def pulseOut outs pulse =
    return [[out, pulse] for out in outs]

def get dict name =
    return dict[name]

def set dict name value =
    dict[name] = value

def readIn ins name =
    if name not in ins:
        ins[name] = []
    return ins[name]

def registerTransition ins fromName toName =
    if toName not in ins:
        ins[toName] = []
    ins[toName].append(fromName)

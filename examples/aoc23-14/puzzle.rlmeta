actor CLI =
    "Args" .:input !. -> put(["File" read(input)])

actor Parser =
    "File" [grid:x] !. -> put(x)
    where
        grid  = line*:xs                  -> ["Grid" ["Columns" ~transpose(xs)]]
        line  = char:x char*:xs '\n'      -> { x xs }
        char  = '#' | '.' | 'O'

def transpose rows =
    assert len(set(len(x) for x in rows)) == 1
    return [
        "".join(rows[row][column] for row in range(len(rows)))
        for column in range(len(rows[0]))
    ]

actor LoadCalculator =
    "Grid" ["Columns" loadColumn*:xs !.] !. -> put(["Loads" ~xs])
    where
        loadColumn = .:x -> calculateLoad(x)

def calculateLoad line =
    return sum(
        len(line)-pos if char == "O" else 0
        for pos, char in enumerate(roll(line))
    )

def roll line =
    newLine = []
    loadSum = 0
    skippedDots = []
    for pos in range(len(line)):
        if line[pos] == "O":
            newLine.append("O")
        elif line[pos] == "#":
            newLine.extend(skippedDots)
            newLine.append("#")
            skippedDots = []
        else:
            skippedDots.append(".")
    newLine.extend(skippedDots)
    assert len(newLine) == len(line)
    return "".join(newLine)

actor Summer =
    "Loads" .*:xs !. -> put(["Result" sum(xs)])

universe Part1 =
    CLI()
    Parser()
    LoadCalculator()
    Summer()
    examples
        ["Args" "example.txt"] -> [["Result" 136]]
        ["Args" "input.txt"] -> [["Result" 105784]]

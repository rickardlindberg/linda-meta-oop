universe Part1 =
    CLI()
    Parser()
    ShortestPathFinder()

actor CLI =
    "Args" .:input !. -> put(["File" read(input)])

actor Parser =
    "File" [map:x] !. -> put(x)
    where
        map   = row*:xs !.     -> ["Map" countColumns(xs) len(xs) xs]
        row   = digit*:xs '\n' -> xs
        digit = '1'-'9':x      -> int(x)
    examples
        ["File" "123\n456\n"] -> [
            ["Map"
                3
                2
                [
                    [1 2 3]
                    [4 5 6]
                ]
            ]
        ]

def countColumns rows =
    columns = [len(x) for x in rows]
    assert len(set(columns))
    return columns[0]

actor ShortestPathFinder =
    "Map" .:width .:height .:map -> put(["Result" astar(
        tuple(0 0)
        tuple(dec(width) dec(height))
        map
    )])
    examples
        ["Map" 3 1 [
            [1 2 1]
        ]] -> [["Result" 3]]
        ["Map" 3 2 [
            [1 9 8]
            [1 1 1]
        ]] -> [["Result" 3]]
        ["Map" 3 3 [
            [1 1 9]
            [9 1 5]
            [9 6 1]
        ]] -> [["Result" 8]]

def astar start goal map =
    estimates = {start: ideal(start, goal)}
    cost = {start: 0}
    cameFrom = {}
    openSet = [start]
    while openSet:
        openSet = sorted(openSet, key=lambda x: estimates[x])
        next = openSet.pop(0)
        x, y = next
        if next == goal:
            return cost[cameFrom[next]] + map[y][x]
        for neighbour in findNeighbours(next, map, cameFrom):
            xn, yn = neighbour
            neighbourCost = cost[next] + map[yn][xn]
            if neighbour not in cost or neighbourCost < cost[neighbour]:
                cameFrom[neighbour] = next
                cost[neighbour] = neighbourCost
                estimates[neighbour] = neighbourCost + ideal(neighbour, goal)
                if neighbour not in openSet:
                    openSet.append(neighbour)
    raise ValueError("no path found")

def findNeighbours pos map cameFrom =
    x, y = pos
    path = reconstruct(pos, cameFrom)
    sameX = len(path) >= 3 and len(set(x for x, y in path[-3:])) == 1
    sameY = len(path) >= 3 and len(set(y for x, y in path[-3:])) == 1
    up = (x, y-1)
    down = (x, y+1)
    left = (x-1, y)
    right = (x+1, y)
    candidates = [up, down, left, right]
    if sameX:
        candidates.remove(up)
        candidates.remove(down)
    if sameY:
        candidates.remove(left)
        candidates.remove(right)
    for candidate in list(candidates):
        xc, yc = candidate
        if xc < 0 or yc < 0 or cameFrom.get(pos) == candidate:
            candidates.remove(candidate)
        else:
            try:
                map[yc][xc]
            except IndexError:
                candidates.remove(candidate)
    return candidates

def reconstruct pos cameFrom =
    path = []
    path.insert(0, pos)
    while pos in cameFrom:
        pos = cameFrom[pos]
        path.insert(0, pos)
    return path

def ideal start end =
    x1, y1 = start
    x2, y2 = end
    return abs(x1-x2) + abs(y1-y2)

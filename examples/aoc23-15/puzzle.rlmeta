universe Part1 =
    CLI()
    Parser()
    Hasher()
    examples
        ["Args" "example.txt"] -> [["Result" 1320]]
        ["Args" "input.txt"] -> [["Result" 518107]]

universe Part2 =
    CLI()
    Parser()
    InstructionParser()
    InstructionRunner()
    BoxSummer()
    examples
        ["Args" "example.txt"] -> [["Result" 145]]
        ["Args" "input.txt"] -> [["Result" 303404]]

actor CLI =
    "Args" .:input !. -> put(["File" read(input)])

actor Parser =
    "File" [sequence:x] !. -> put(x)
    where
        sequence  = step:x (',' step)*:xs !. -> ["Sequence" x ~xs]
        step      = char*:xs                 -> [~~xs]
        char      = !',' ('\n' -> [] | .:x   -> [x])
    examples
        ["File" "rn=1,cm-"] -> [
            ["Sequence"
                ["r" "n" "=" "1"]
                ["c" "m" "-"]
            ]
        ]
        ["File" "ot=7\n"] -> [
            ["Sequence"
                ["o" "t" "=" "7"]
            ]
        ]

actor Hasher =
    "Sequence" hash*:xs !. -> put(["Result" sum(xs)])
    where
        hash = [.*:xs] -> hashAlgorithm(xs)
    examples
        ["Sequence" ["H"]] -> [["Result" 200]]
        ["Sequence" ["H" "A" "S" "H"]] -> [["Result" 52]]
        ["Sequence" ["o" "t" "=" "7"]] -> [["Result" 231]]
        ["Sequence" ["r" "n"]] -> [["Result" 0]]

def hashAlgorithm characters =
    result = 0
    for character in characters:
        result += ord(character)
        result *= 17
        result = result % 256
    return result

actor InstructionParser =
    "Sequence" operation*:xs !. -> put(["Operations" ~xs])
    where
        operation = [label:x action:y !.] -> [x y]
        label     = char:x char*:xs       -> [x ~xs]
        char      = !('=' | '-') .
        action =
            | '=' number:x -> ["set" x]
            | '-'          -> ["remove"]
        number    = digit:x digit*:xs     -> int({x xs})
        digit     = '0'-'9'
    examples
        ["Sequence"
            ["r" "n" "=" "1"]
            ["c" "m" "-"]
        ] -> [["Operations"
            [["r" "n"] ["set" 1]]
            [["c" "m"] ["remove"]]
        ]]

actor InstructionRunner =
    "Operations" operation*:xs !.
        -> dict():boxes
        -> xs
        -> put(["Boxes" boxes])
    where
        operation = [[.*:label] action:y !.]
            -> hashAlgorithm(label):boxId
            -> { label }:label
            -> y
        action =
            | ["set" .:value] -> boxSet(boxes boxId label value)
            | ["remove"]      -> boxRemove(boxes boxId label)
    examples
        ["Operations"
            [["r" "n"] ["set" 1]]
            [["c" "m"] ["remove"]]
            [["q" "p"] ["set" 3]]
            [["c" "m"] ["set" 2]]
        ] -> [["Boxes" dict([
            [0 [["rn" 1] ["cm" 2]]]
            [1 [["qp" 3]]]
        ])]]

def boxSet boxes boxId label value =
    if boxId in boxes:
        if label in [x[0] for x in boxes[boxId]]:
            boxes[boxId] = [
                x if x[0] != label else [label, value]
                for x in boxes[boxId]
            ]
        else:
            boxes[boxId] = boxes[boxId] + [[label, value]]
    else:
        boxes[boxId] = [[label, value]]

def boxRemove boxes boxId label =
    if boxId in boxes:
        boxes[boxId] = [
            x
            for x in boxes[boxId]
            if x[0] != label
        ]

actor BoxSummer =
    "Boxes" .:x !. -> put(["Result" sumBoxes(x)])
    examples
        ["Boxes" dict([[0 [["rn" 5]]]])] -> [["Result" 5]]
        ["Boxes" dict([[3 [["ot" 7]]]])] -> [["Result" 28]]

def sumBoxes boxes =
    result = 0
    for boxId, lenses in boxes.items():
        for slotId, (lable, value) in enumerate(lenses):
            result += (boxId+1)*(slotId+1)*value
    return result

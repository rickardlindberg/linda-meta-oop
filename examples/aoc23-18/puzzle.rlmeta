universe Part1 =
    CLI()
    Parser()

actor CLI =
    "Args" .:input !. -> put(["File" read(input)])

actor Parser =
    "File" [plan:x] !. -> put(x)
    where
        plan        = instruction*:xs !.   -> ["Plan" ~xs]
        instruction =
            direction:d ' '
            number:n ' '
            color:c '\n'
                -> [d n c]
        direction   = 'U' | 'D' | 'L' | 'R'
        number      = digit:x digit*:xs    -> int({ x xs })
        digit       = '1'-'9':x            -> int(x)
        color       = '(' (!')' .)*:xs ')' -> { xs }
    examples
        ["File" "R 9 (#066240)\nU 6 (#113213)\n"] -> [
            ["Plan"
                ["R" 9 "#066240"]
                ["U" 6 "#113213"]
            ]
        ]

actor Digger =
    "Plan" instruction*:xs !.
        -> dict([tuple("x" 0) tuple("y" 0)]):pos
        -> put(["Polygon" render(pos) ~xs])
    where
        instruction =
            | ["U" .:n] -> move(pos 0         negate(n))
            | ["D" .:n] -> move(pos 0         n)
            | ["L" .:n] -> move(pos negate(n) 0)
            | ["R" .:n] -> move(pos n         0)
    examples
        ["Plan"
            ["R" 2 "red"]
            ["D" 2 "green"]
        ] -> [["Polygon"
            tuple(0 0)
            tuple(2 0)
            tuple(2 2)
        ]]

def render pos =
    return (pos["x"], pos["y"])

def move pos dx dy =
    pos["x"] += dx
    pos["y"] += dy
    return render(pos)

def negate x =
    return -1*x

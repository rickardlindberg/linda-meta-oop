universe Part1 =
    CLI()
    Parser()
    ShortestPathFinder()
    examples
        ["Args" "example.txt"] -> [["Result" 102]]

actor CLI =
    "Args" .:input !. -> put(["File" read(input)])

actor Parser =
    "File" [map:x] !. -> put(x)
    where
        map   = row*:xs !.     -> ["Map" countColumns(xs) len(xs) xs]
        row   = digit*:xs '\n' -> xs
        digit = '1'-'9':x      -> int(x)
    examples
        ["File" "123\n456\n"] -> [
            ["Map"
                3
                2
                [
                    [1 2 3]
                    [4 5 6]
                ]
            ]
        ]

def countColumns rows =
    columns = [len(x) for x in rows]
    assert len(set(columns))
    return columns[0]

actor ShortestPathFinder =
    "Map" .:width .:height .:map -> put(["Result" astar(
        tuple(0 0)
        tuple(dec(width) dec(height))
        map
    )])
    examples
        ["Map" 3 1 [
            [1 2 1]
        ]] -> [["Result" 3]]
        ["Map" 5 1 [
            [1 1 1 1 1]
        ]] -> [["Result" "no path found"]]
        ["Map" 5 2 [
            [1 1 1 1 1]
            [1 1 1 9 1]
        ]] -> [["Result" 7]]
        ["Map" 3 2 [
            [1 9 8]
            [1 1 1]
        ]] -> [["Result" 3]]
        ["Map" 3 3 [
            [1 1 9]
            [9 1 5]
            [9 6 1]
        ]] -> [["Result" 8]]

def astar start goal map =
    estimates = {start: ideal(start, goal)}
    cost = {start: 0}
    cameFrom = {}
    openSet = [start]
    while openSet:
        openSet = sorted(openSet, key=lambda x: estimates[x])
        next = openSet.pop(0)
        x, y = next[:2]
        if next[:2] == goal[:2]:
            return cost[next]
        for neighbour in findNeighbours(next, map, cameFrom):
            xN, yN = neighbour[:2]
            neighbourCost = cost[next] + map[yN][xN]
            if neighbour not in cost or neighbourCost < cost[neighbour]:
                cameFrom[neighbour] = next
                cost[neighbour] = neighbourCost
                estimates[neighbour] = neighbourCost + ideal(neighbour, goal)
                if neighbour not in openSet:
                    openSet.append(neighbour)
    return "no path found"

actor NeighbourFinder =
    "FindNeighbour" .:pos .:map .:cameFrom
        -> put(findNeighbours(pos map cameFrom))
    examples
        ["FindNeighbour"
            tuple(3 0 "up" "right" "right")
            [
                [1 1 1 1 1]
                [1 1 1 1 1]
            ]
            dict([
                tuple(tuple(3 0 "up" "right" "right") tuple(2 0 "right"))
            ])
        ] -> [[
            tuple(3 1 "right" "right" "down")
            tuple(4 0 "right" "right" "right")
        ]]

def findNeighbours node map cameFrom =
    x, y = node[:2]
    up    = (x,   y-1)+node[-2:]+("up",)
    down  = (x,   y+1)+node[-2:]+("down",)
    left  = (x-1, y, )+node[-2:]+("left",)
    right = (x+1, y, )+node[-2:]+("right",)
    if len(set(node[-3:])) == 1:
        repeatedDir = node[-1]
    else:
        repeatedDir = None
    candidates = [up, down, left, right]
    if repeatedDir in ["up", "down"]:
        candidates.remove(up)
        candidates.remove(down)
    if repeatedDir in ["left", "right"]:
        candidates.remove(left)
        candidates.remove(right)
    for candidate in list(candidates):
        xc, yc = candidate[:2]
        if xc < 0 or yc < 0 or cameFrom.get(node, (None, None))[:2] == candidate[:2]:
            candidates.remove(candidate)
        else:
            try:
                map[yc][xc]
            except IndexError:
                candidates.remove(candidate)
    return candidates

def reconstruct node cameFrom =
    path = [node[:2]]
    while node in cameFrom:
        node = cameFrom[node]
        path.insert(0, node[:2])
    return path

def ideal start end =
    x1, y1 = start[:2]
    x2, y2 = end[:2]
    return abs(x1-x2) + abs(y1-y2)
